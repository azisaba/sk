on tool change:
    "%world%" starts with "lobby" # develop

    if player's held item is feather:
        send "&6二段ジャンプが可能になりました"

on jump:
    "%world%" starts with "lobby" # develop

    if player's held item is feather:
        set player's flight mode to true

on flight toggle:
    "%world%" starts with "lobby" # develop

    # クリエイティブのフライを許可
    if player's gamemode is survival:
        # 少し待機しないとたまに飛べなくなる
        wait a tick

        set player's flight mode to false

    if player's held item is feather:
        cancel event

        play sound "ENTITY_BAT_TAKEOFF" with pitch 0.6

        # 向いている方向に飛ばす
        push player in direction of player at speed 1
        # 上方向に強めに飛ばす
        push player upwards at speed 2

        send action bar "%player's velocity%" to player

on damage:
    "%world%" starts with "lobby" # develop

    if victim's held item is feather:
        # 落下ダメージ無効化
        if damage cause is fall:
            cancel event
